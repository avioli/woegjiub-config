" Add My Settings Folder and make vim respect XDG Base Directories
	set rtp=$XDG_CONFIG_HOME/vim,$VIM,$VIMRUNTIME
	set undodir=$XDG_DATA_HOME/vim/undo
	set viewdir=$XDG_DATA_HOME/vim/view
	let g:netrw_home = expand('$XDG_DATA_HOME/vim/netrw')

" Addon Settings
	" Set up Vundle to do its magic
		filetype off
		let mapleader = ","
		set rtp+=$XDG_DATA_HOME/vim/bundle/vundle
		call vundle#rc()
		let g:bundle_dir = expand('$XDG_DATA_HOME/vim/bundle')
		Plugin 'gmarik/vundle'
	" Bundle and Configure Addons ($lang conf in 'settings/ftplugin/$lang.vim')
		" Documentation
			" Python
				Plugin 'fs111/pydoc.vim'
		" Git
			Plugin 'tpope/vim-fugitive'
			Plugin 'tpope/vim-git'
		" Interface/Navigation addons
			" Database interface
				Plugin 'dbext.vim'
				let g:dbext_default_history_file = expand('$XDG_DATA_HOME/vim/dbhist')
				let g:dbext_default_history_size = 5000
				let dbext_default_user = ""
				let dbext_default_passwd = ""
				let dbext_default_window_use_horiz = 0
				let dbext_default_type = 'SQLITE'
				let dbext_default_dbname = 'tmp.sqlite'
				let dbext_default_window_width = 100
				let g:dbext_default_SQLITE_bin = 'sqlite3'
			" Note Taking
				Plugin 'vimoutliner/vimoutliner'
					let maplocalleader=",,"
					" Mappings to change code to and from outlines
					map <leader>oc :s/^/<\ /<cr>:noh<cr>
					map <leader>oC :s/^	*<\ \=//<cr>:noh<cr>
					" Mapping to create paragraphs from visual blocks of
					" HTML paragraphs pasted over
					map <leader>op :s/^\(\t*:\)\(.\+\)/\1____\2/<cr>gvgqgv:s/____/\ \ \ \ /g<cr>gv:s/^\t*:\s*\n//<cr>:noh<cr>
					" Mapping to wrap citations
					map <leader>oq :s/^\(\t*\:\)\(\d\+\)/\1\ \ \ \ \2/<cr>gvgqgv:s/^\(\t*:\)\ \ \ \ \(\d\+\.*\ \)/\1\2/<cr>gv:s/^\(\t*\):/\1</<cr>gv:s/^\t*<\s*\n//<cr>:noh<cr>
					" Mapping to swap from wrapping to static text in V-block
					map <leader>os :s/^\(\t*\):/\1</<cr>:noh<cr>
			" Text alignment
				Plugin 'godlygeek/tabular'
				Plugin 'maksimr/vim-jsbeautify'
				map <leader>tj :call RangeJsBeautify()<cr>
			" Revision History
				Plugin 'sjl/gundo.vim'
					map <leader>g :GundoToggle<CR>
					let g:gundo_preview_bottom = 1
					let g:gundo_preview_height = 30
			" File Management
				Plugin 'tpope/vim-vinegar'
					map <leader>n :Explore<CR>
					let g:netrw_liststyle = 3
			" Tag outliner
				Plugin 'majutsushi/tagbar'
					map <leader>m :TagbarToggle<CR>
			" List toggle
				Plugin 'Valloric/ListToggle'
			" Shell
				Plugin 'rosenfeld/conque-term'
			" Show extended information when pressing 'ga' on a character
				Plugin 'tpope/vim-characterize'
			" Sexy statusbar
				Plugin 'bling/vim-airline'
				let g:airline#extensions#tabline#enabled = 1
				let g:airline#extensions#tabline#fnamemod = ':t'
				let g:airline_left_sep=''
				let g:airline_left_alt_sep=''
				let g:airline_right_sep=''
				let g:airline_right_alt_sep=''
		" Search Addons
			" Enable Ack Searching
				Plugin 'mileszs/ack.vim'
					nmap <leader>a <Esc>:Ack! 
			" Search Buffers/Filenames+Paths for navigation
				Plugin 'kien/ctrlp.vim'
				let g:ctrlp_custom_ignore = {
					\ 'dir': '\.git$\|\.svn$\|\.hg$\|build$\|venv\|^output$',
					\ 'file': '\.pyc$\|\.so$\|\.class$\|.swp$\|\.pid\|\.beam$',
					\ }
				map <leader>b :CtrlPBuffer<cr>
				map <leader>f :CtrlPMRU<cr>
				let g:ctrlp_match_window = 'max:30'
		" Syntax Highlighting
			" CSS3
				Plugin 'hail2u/vim-css3-syntax'
				Plugin 'ap/vim-css-color'
			" HTML5
				Plugin 'othree/html5.vim'
			" JINJA
				Plugin 'lepture/vim-jinja'
			" Javascript
				Plugin 'jelera/vim-javascript-syntax'
				Plugin 'pangloss/vim-javascript'
			" JSON
				Plugin 'elzr/vim-json'
			" LESS
				Plugin 'groenewege/vim-less'
			" Prolog
				Plugin 'adimit/prolog.vim'
			" CSV
				Plugin 'csv.vim'
		" Syntax Testing
			Plugin 'scrooloose/syntastic'
				let g:syntastic_auto_loc_list=2
				let g:syntastic_stl_format = '[%E{Err: %fe #%e}%B{, }%W{Warn: %fw #%w}]'
				let g:syntastic_check_on_wq = 0
				let g:syntastic_aggregate_errors = 1
				let g:syntastic_always_populate_loc_list = 1
				map <F5> :SyntasticCheck<cr>
			" Javascript
				Plugin 'marijnh/tern_for_vim'
			" Python
				Plugin 'alfredodeza/pytest.vim'
		" Typing Efficiency Improvers
			" Snippets
				Plugin 'SirVer/ultisnips'
				Plugin 'honza/vim-snippets'
				let g:UltiSnipsExpandTrigger="zz"
			" Change the Surroundings of Text
				Plugin 'tpope/vim-surround'
			" Semantic completion
				Plugin 'Valloric/YouCompleteMe'
				nmap <leader>jg :YcmCompleter GoToDefinition<cr>
				nmap <leader>jd :YcmCompleter GoToDeclaration<cr>
				let g:ycm_complete_in_strings = 1
				let g:ycm_seed_identifiers_with_syntax = 1
				let g:ycm_autoclose_preview_window_after_completion = 1
				let g:ycm_autoclose_preview_window_after_insertion = 1
				let g:ycm_max_diagnostics_to_display = 100
			" Quicker Commenting/Uncommenting
				Plugin 'tpope/vim-commentary'
			" Add repeat . to addons and unrepeatable vanilla commands
				Plugin 'tpope/vim-repeat'
			" Zencoding for HTML
				Plugin 'mattn/emmet-vim'
				let g:user_emmet_leader_key = '\'
				let g:user_emmet_complete_tag = 1

" Indentation Settings
	set shiftround							" Indents rounded to shiftwidths
	set copyindent							" Maintain current indent on next line
	set preserveindent						" Try not to mess with indentation
	set autoindent							" Enables autoindent
	set smartindent							" Enables smartindent
	set shiftwidth=4						" Indentation at 4 width
	set tabstop=4							" Tabs at 4 width
	set softtabstop=4						" Soft tabs at 4 width
	set textwidth=79						" Line wrap at 80 characters
	if v:version >= 703
		set colorcolumn=+1					" Colours the column following that
	endif
	set fo+=t								" Enables autowrap
	set wrap								" Enables line wrapping for long lines
	set breakindent							" For soft wraps, indent newlines
	filetype on								" Enable filetype-specific settings
	filetype plugin indent on				" Enable filetype-specific indentation
	set list								" Show whitespace
	set listchars=tab:│\ ,trail:-			" Show these whitespaces by default
	let g:pyindent_open_paren = '&sw'		" Indent 1 sw after ( in python
	let g:pyindent_nested_paren = '&sw'		" Indent 1 sw after nested () in py
	let g:pyindent_continue = '&sw'			" Indent 1 sw for continuation

" Folding settings
	set foldmethod=indent					" Enable Folding
	set foldlevelstart=99					" Start at fold level 99
	" Save fold settings
		au BufWinLeave *.* mkview!
		au BufWinEnter *.* silent loadview

" Search settings
	set ignorecase							" Ignores cases in searching
	set smartcase							" Smart case matching
	set hlsearch							" Highlight search results
	set incsearch							" Search incrementally

" General settings
	set ffs=unix							" Unix as standard fileformat
	set encoding=utf-8						" UTF-8 as standard encoding
	set nocompatible						" Enable vimness
	set autoread							" Automatically detect outside changes
	set history=1000						" Massive history
	set completeopt=menuone,longest,preview	" Set autocompletion options
	set omnifunc=syntaxcomplete#Complete	" Enable Omnicompletion
	set exrc								" Per-directory vimrcs
	set secure								" Disable insecure local vimrc commands
	set modeline							" Vim settings in files
	set noswapfile							" Stop littering these things
	set viminfo+=n$XDG_DATA_HOME/vim/history	" Less things in home
	set tabpagemax=100
	set spelllang=en_au,de					" English-Aus, German for words
	set clipboard=unnamedplus				" Use system clipboard by default
	set undofile							" Enable persistent undo

" UI Settings
	set showcmd								" Shows Current Command
	set showfulltag							" Increase info for tag completion
	set ruler								" Show position at bottom
	set number								" I need line numbers
	set lazyredraw							" Do not redraw whilst executing macros
	set noerrorbells novisualbell			" Shut the warnings up
	syntax on								" Enable syntax highlighting
	set showmatch							" Highlight matching brackets
	set mat=2								" Blink that highlight at 0.2s
	set wildmenu							" Good menu tab completion
	set wildmode=list:longest,full			" Set menu completion style
	set wildignore=*.o,*~,~.pyc				" Ignore compiled files
	au BufReadPost *						" Go to last position on file open
	\ if line("'\"") > 0 && line("'\"") <= line("$") |
	\	exe "normal! g`\"" |
	\ endif
	set laststatus=2						" Always show statusline
	" file, helpfile, modified, readonly, split,  cursor location, V%
	set statusline=%<%f\ %h%m%r%{SyntasticStatuslineFlag()}%=%-14.(%l,%c,%V%)\ %P
	set t_Co=256							" 256 Colours in Terminal
	colo darko								" Load my colourscheme

" Overrides
	au BufNewFile,BufRead *.html set filetype=htmldjango " Presume html.django

" Input/Hotkey settings
	set mouse=a								" Enable Mouse
	" Stop mousewheel and clicking from inserting letters
	if has("mouse_sgr")
		set ttymouse=sgr
	else
		set ttymouse=xterm2
	end
	set backspace=eol,start,indent			" Proper backspace behaviour
	" tabbing
		map <C-t> :enew<cr>
		map gt :bnext<cr>
		map gT :bprevious<cr>
		map :Q :bd
	let g:clipbrdDefaultReg="+"				" Use Linux clipboard as def copy reg
	" easy buffer navigation
		map <leader>B :buffers<CR>:buffer 
	" Make shift-insert work as linux paste
		map <S-Insert> <MiddleMouse>
		map! <S-Insert> <MiddleMouse>
	" Recall recent file list
		map <leader>r :browse oldfiles<CR>
	" Fix control-arrows in tmux
	if &term =~ '^screen'
		execute "set <xUp>=\e[1;*A"
		execute "set <xDown>=\e[1;*B"
		execute "set <xRight>=\e[1;*C"
		execute "set <xLeft>=\e[1;*D"
	endif
	" Move lines around with C_↓ or C_↑
		nmap <silent> <c-down> mz:m+<CR>`z
		nmap <silent> <c-up> mz:m-2<CR>`z
		vmap <silent> <c-down> :m'>+<CR>`<my`>mzgv`yo`z
		vmap <silent> <c-up> :m'<-2<CR>`>my`<mzgv`yo`z
	" Move splits around with C_h/j/k/l
		map <c-l> <c-w>l
		map <c-h> <c-w>h
		map <c-k> <c-w>k
		map <c-j> <c-w>j
	" Map hard BOL to soft BOL
		map 0 ^
	" Open with file's directory filled in
		map <leader>e :e <C-R>=expand('%:p:h') . '/'<CR>
	" insert current file's directory in prompt
		cabbr <expr> %% expand('%:p:h')
	" Swap braces to/from being on their own line (c-like languages)
		function! MoveBraces(brace)
			let searchString = '\n\s*'.a:brace
			if search(searchString)
				exec '%s/'.searchString.'/'.a:brace.'/g'
			else
				if a:brace ==# "}"
					exec 'g!/[^;]}\+;/s/}/\r}/g'
				elseif a:brace ==# "{"
					exec '%s/{$/\r{/g'
				endif
				normal gg=G
			endif
		endfunction
		map <leader>{ :call MoveBraces("{")<CR>
		map <leader>} :call MoveBraces("}")<CR>
	" Clear search results
		map <leader>cS :noh<cr>
	" Centers Search results
		nnoremap n nzz
		nnoremap N Nzz
		nnoremap * *zz
		nnoremap # #zz
		nnoremap g* g*zz
		nnoremap g# g#zz
	" Show word's syntax group with C_S_p
		nmap <C-S-S> :call <SID>SynStack()<CR>
		function! <SID>SynStack()
			if !exists("*synstack")
				return
			endif
			echo map(synstack(line('.'), col('.')), 'synIDattr(v:val, "name")')
		endfunc

" Session Management
	" Save
		func! <SID>Savesession()
			if v:this_session == ""
				let cmd = "mksession! $XDG_DATA_HOME/vim/sessions/default.vim"
			else
				let cmd = "mksession! ".v:this_session
			endif
			echo cmd
			exec cmd
		endfunc
		nmap <leader>sw <ESC>:call <SID>Savesession()<cr>:wa<cr>
	" Save+Quit
		nmap <leader>sq <ESC>:call <SID>Savesession()<cr>:wqa<cr>
	" Save As
		nmap <leader>sa :wa<cr>:mksession! $XDG_DATA_HOME/vim/sessions/
	" Restore As (Open)
		nmap <leader>so :so $XDG_DATA_HOME/vim/sessions/

" Saving Improvements
	" Use ,W to save the current file with sudo
		nmap <leader>W :w !sudo tee %<cr><cr>
	" Create directories if they don't exist
		function s:MkNonExDir(file, buf)
		if empty(getbufvar(a:buf, '&buftype')) && a:file!~#'\v^\w+\:\/'
			let dir=fnamemodify(a:file, ':h')
			if !isdirectory(dir)
				call mkdir(dir, 'p')
			endif
		endif
		endfunction
		augroup BWCCreateDir
			autocmd!
			autocmd BufWritePre * :call s:MkNonExDir(expand('<afile>'), +expand('<abuf>'))
		augroup END
